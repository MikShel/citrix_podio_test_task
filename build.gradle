apply plugin: 'java'

repositories {
    mavenCentral()
    //mavenLocal()
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

def login = hasProperty('login') ? login : 'no'
def pass = hasProperty('pass') ? pass : 'no'
def environment = hasProperty('env') ? env : 'dev'     //(run tests on specific environment)
def removeLeadingSpaces = { it.trim() }
def nonBlankLines = { it }
def tags = hasProperty('tags') ? tags : ''             //(run scenarios with specific tag)
def feature = hasProperty('feature') ? feature : ''    //(run specific feature)
def url = hasProperty('url')  ? url : 'https://nextpodio.dk'                         //(run tests on custom url)
def browser = hasProperty('browser') ? browser : 'pjs'  //(run tests in specific  browser)
def debug = hasProperty('debug') ? debug : 'false'     //(enable debug mode for tests)


tags = tags.split(",").collect(removeLeadingSpaces).findAll(nonBlankLines)
feature = feature.split(",").collect(removeLeadingSpaces).findAll(nonBlankLines)

def tagsArgs = tags.collectMany({ ['--tags', it] })
def variableArgs = feature.collectMany({ ['--name', it] })

def permanentArgs = [
        '--monochrome', '--format', 'pretty', '--format', 'junit:build/cucumber-report.xml',
        '--glue', 'podio.test.step_definitions', 'src/test/resources'
]

permanentArgs += variableArgs + tagsArgs


task cucumber(){
    dependsOn assemble, compileJava, compileTestJava
    doLast {

        javaexec {
            main = "cucumber.api.cli.Main"
            debug=debug
            systemProperties = [
                    'url': url,
                    'browser': browser,
                    'login': login,
                    'pass': pass]
            classpath = configurations.cucumberRuntime + sourceSets.test.output + sourceSets.main.output
            args = permanentArgs
        }
    }
}

dependencies {

    compile "info.cukes:cucumber-core:1.1.8"
    compile "info.cukes:cucumber-java:1.1.8"
    compile "info.cukes:cucumber-junit:1.1.8"
    compile "ru.yandex.qatools.htmlelements:htmlelements-java:1.12"
    compile "ru.yandex.qatools.htmlelements:htmlelements-matchers:1.12"
    compile "org.seleniumhq.selenium:selenium-java:2.43.1"
    compile "junit:junit:4.11"
    compile "com.github.detro.ghostdriver:phantomjsdriver:1.1.0"
    compile "org.slf4j:slf4j-api:1.7.7"
    compile "com.google.guava:guava:18.0"
    //compile "com.google.api-client:google-api-client:1.18.0-rc"

}